Program         = { Statement } ;
Statement       = Assignment | IfStatement | WhileStatement | FunctionCall ;
Assignment      = Identifier, "=", Expression, ";" ;
IfStatement     = "if", "(", Condition, ")", "{", { Statement }, "}", [ "else", "{", { Statement }, "}" ] ;
WhileStatement  = "while", "(", Condition, ")", "{", { Statement }, "}" ;
FunctionCall    = Identifier, "(", [ ArgumentList ], ")", ";" ;
ArgumentList    = Expression, { ",", Expression } ;
Expression      = Term, { ("+" | "-"), Term } ;
Term            = Factor, { ("*" | "/"), Factor } ;
Factor          = Number | Identifier | "(", Expression, ")" ;
Condition       = Expression, ("==" | "!=" | "<" | ">" | "<=" | ">="), Expression ;
Identifier      = Letter, { Letter | Digit } ;
Number          = Digit, { Digit } ;
Letter          = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" |"A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ;
Digit           = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;